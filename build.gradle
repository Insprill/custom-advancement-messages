plugins {
    id 'java'
    id "io.freefair.lombok" version "6.3.0"
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group 'net.insprill'
version '1.0.4.3-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    mavenLocal()
}

dependencies {
    compileOnly 'me.clip:placeholderapi:2.11.1'
    compileOnly 'org.jetbrains:annotations:23.0.0'
    compileOnly 'org.spigotmc:spigot-api:1.18.1-R0.1-SNAPSHOT'
    compileOnly('com.github.MilkBowl:VaultAPI:1.7') { transitive = false }
    implementation 'net.insprill:xenlib:a6257bb430'
    implementation 'org.bstats:bstats-bukkit:3.0.0'
}

shadowJar {
    archiveClassifier.set('')

    relocate("net.insprill.xenlib", "net.insprill.cam.libs.net.insprill.xenlib")
    relocate("org.bstats", "net.insprill.cam.libs.org.bstats")

    minimize()
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

def targetJavaVersion = 17
java {
    compileJava.options.encoding = 'UTF-8'
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}
