plugins {
    id 'java'
    id "io.freefair.lombok" version "6.5.1"
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'com.rikonardo.papermake' version '1.0.4'
}

group 'net.insprill'
version '1.0.4.3-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17
compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
    mavenLocal()
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    compileOnly 'me.clip:placeholderapi:2.11.2'
    compileOnly 'org.jetbrains:annotations:23.0.0'
    compileOnly 'org.spigotmc:spigot-api:1.19-R0.1-SNAPSHOT'
    compileOnly('com.github.MilkBowl:VaultAPI:1.7.1') { transitive = false }
    implementation 'net.insprill:xenlib:8983a49a4b'
    implementation 'org.apache.commons:commons-text:1.10.0'
    implementation 'org.bstats:bstats-bukkit:3.0.0'
}

shadowJar {
    archiveClassifier.set('')

    var libPackage = "net.insprill.cam.libs"
    relocate("net.insprill.xenlib", "${libPackage}.net.insprill.xenlib")
    relocate("org.apache.commons.text", "${libPackage}.org.apache.commons.text")
    relocate("org.bstats", "${libPackage}.org.bstats")

    minimize()
}
build.dependsOn(shadowJar)

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
